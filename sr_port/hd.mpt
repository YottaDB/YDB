;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;								;
; Copyright 1987,2001 Sanchez Computer Associates, Inc.		;
;								;
; Copyright (c) 2017 YottaDB LLC. and/or its subsidiaries.	;
; All rights reserved.						;
;								;
;	This source code contains the intellectual property	;
;	of its copyright holder(s), and is made available	;
;	under a license.  If you do not know the terms of	;
;	the license, please stop and do not read further.	;
;								;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
%HD	;GT.M %HD utility - hexadecimal to decimal conversion program
	;invoke at INT with %HD in hexadecimal to return %HD in decimal
	;invoke at FUNC as an extrinsic function
	;Note: Ignores any prefix of 0x or 0X
	;if you make heavy use of this routine, consider $ZCALL
	;
	s %HD=$$FUNC(%HD)
	q
INT	r !,"Hexidecimal: ",%HD s %HD=$$FUNC(%HD)
	q
FUNC(h)
	n c,d,dg,l
	s d=0,h=$tr(h,"abcdefx","ABCDEFX"),l=$l(h)
	s:("0X"=$e(h,1,2)) h=($zextract(h,3,l)),l=l-2
	f c=1:1:l s dg=$f("0123456789ABCDEF",$e(h,c)) q:'dg  s d=(d*16)+(dg-2)
	q d
